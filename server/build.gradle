//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'java'
	id 'application'
}

sourceSets {
	main {
		java {
			srcDir 'src'
		}
	}

	test {
		java {
			srcDir 'test'
		}
	}
}

configurations {
	hsqldb
}

dependencies {
	hsqldb 'org.hsqldb:hsqldb:2.6.0'
	implementation configurations.hsqldb.dependencies
	testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
	/* Apache Cassandra driver https://github.com/datastax/java-driver */
}

java {
	sourceCompatibility JavaVersion.VERSION_16
}

compileJava {
	options.compilerArgs += '--enable-preview'
}

compileTestJava {
	options.compilerArgs += '--enable-preview'
}

application {
	mainModule = 'ritzow.notes.server'
	mainClass = 'ritzow.notes.server.RunServer'
}

run {
	doLast {
		workingDir project.file('server')
	}

	jvmArgs += '--enable-preview'
}

test {
	useJUnitPlatform()
	//reports.html.required = false
	jvmArgs += '--enable-preview'
}

task databaseManager(type: JavaExec) {
	group 'application'
	classpath configurations.hsqldb.asPath
	mainClass = 'org.hsqldb.util.DatabaseManagerSwing'
	args "--url", "jdbc:hsqldb:hsql://[::1]/notes"
}